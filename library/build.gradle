apply plugin: 'android-library'
apply plugin: 'maven'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 19
        versionCode project.versionCode
        versionName project.version
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task generateJavadoc(type: Javadoc) {
    source android.sourceSets.main.allJava
}

task generateJavadocJar(type: Jar, dependsOn: generateJavadoc) {
    classifier 'javadoc'
    from generateJavadoc.destinationDir
}

task generateSourceJar(type: Jar) {
    classifier 'src'
    from android.sourceSets.main.allSource
}

artifacts {
    archives generateSourceJar
    archives generateJavadocJar
}

task installLocal(type: Upload) {
    repositories.mavenInstaller {
        configuration = configurations.archives
    }
}
